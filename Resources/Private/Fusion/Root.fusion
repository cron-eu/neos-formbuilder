include: resource://Neos.Fusion/Private/Fusion/*
include: resource://Neos.Neos/Private/Fusion/*

prototype(CRON.FormBuilder:FormElement) < prototype(Neos.Neos:Content) {
	templatePath = ${'resource://CRON.FormBuilder/Private/Templates/NodeTypes/' + String.replace(node.nodeData.nodeType, 'CRON.FormBuilder:', '') + '.html'}

	layoutName = 'Wrapper'

	layoutAttributes = Neos.Fusion:Attributes
	layoutAttributes.class = ${'form-group' + (q(node).property('required') ? ' required' : '')}
	layoutAttributes.class.@process.nodeType = Neos.Fusion:Case {
		@context.nodeTypeClassName = ${String.toLowerCase(String.pregReplace(q(node).property('_nodeType.name'), '/[[:^alnum:]]/', '-')) + '-layout'}

		classIsString {
			condition = ${Type.isString(value)}
			renderer = ${String.trim(value) + ' ' + nodeTypeClassName}
		}

		classIsArray {
			condition = ${Type.isArray(value)}
			renderer = ${Array.push(value, nodeTypeClassName)}
		}
	}

	value = ${request.internalArguments.__submittedArguments.data[node.identifier]}
	value.@if.valueWasSubmitted = ${request.internalArguments.__submittedArguments.data[node.identifier]}
	multiple = ${q(node).property('multiple')}
	formName = ${'--cron_formbuilder-plugin[data][' + node.identifier + ']' + (q(node).property('multiple') ? '[]' : '')}

	attributes.id = ${node.identifier}
	attributes.name = ${'--cron_formbuilder-plugin[data][' + node.identifier + ']' + (q(node).property('multiple') ? '[]' : '')}
	attributes.required = ${q(node).property('required')}
	attributes.required.@if.isRequired = ${Configuration.setting('CRON.FormBuilder.View.useRequiredAttribute') && q(node).property('required')}
	attributes.class = 'form-control'
}

prototype(CRON.FormBuilder:CheckBox) < prototype(CRON.FormBuilder:FormElement) {
	layoutName = 'CheckboxWrapper'
	attributes.class = ''
	checked = ${this.value || q(node).property('checked')}
}

prototype(CRON.FormBuilder:CheckBoxEnhanced) < prototype(CRON.FormBuilder:FormElement) {
	layoutName = 'CheckboxWrapper'
	attributes.class = ''
	checked = ${this.value || q(node).property('checked')}
	label = ${q(node).property('label')}
}

prototype(CRON.FormBuilder:CheckBoxGroup) < prototype(CRON.FormBuilder:FormElement) {

	@context.groupValue = ${this.value}
	@context.groupName = ${this.formName}

	attributes.name >
	attributes.class >

	elements = Neos.Neos:ContentCollection {
		nodePath = 'elements'

		prototype(CRON.FormBuilder:CheckBox) {
			layoutAttributes.class = ''
			attributes.name = ${groupName}
			checked = ${groupValue ? Array.indexOf(groupValue, q(node).property('value')) > -1 : q(node).property('checked')}
		}
	}
}

prototype(CRON.FormBuilder:Input) < prototype(CRON.FormBuilder:FormElement) {

}

prototype(CRON.FormBuilder:Option) < prototype(CRON.FormBuilder:FormElement) {
}

prototype(CRON.FormBuilder:Select) < prototype(CRON.FormBuilder:FormElement) {

	@context.selectValue = ${this.value}
	@context.selectMultiple = ${this.multiple}

	elements = Neos.Neos:ContentCollection {
		nodePath = 'elements'

		prototype(CRON.FormBuilder:Option) {
			selected = ${selectValue ? (selectMultiple ? Array.indexOf(selectValue, q(node).property('value')) > -1 : selectValue == q(node).property('value')) : q(node).property('selected')}
		}
	}
}

prototype(CRON.FormBuilder:TextArea) < prototype(CRON.FormBuilder:FormElement) {
}

prototype(CRON.FormBuilder:FieldSet) < prototype(CRON.FormBuilder:FormElement) {

	attributes.name >
	attributes.class >

	elements = Neos.Neos:ContentCollection {
		nodePath = 'elements'
	}
}

prototype(CRON.FormBuilder:FileUpload) < prototype(CRON.FormBuilder:FormElement) {
	allowedMimeTypes = ${Array.join(Configuration.setting('CRON.FormBuilder.Upload.allowedMimeTypes'), ',')}
}

prototype(CRON.FormBuilder:Text) < prototype(CRON.FormBuilder:FormElement) {
	text = ${q(node).property('text')}
}

prototype(CRON.FormBuilder:Plugin) < prototype(Neos.Neos:Plugin) {
	package = 'CRON.FormBuilder'
	controller = 'FormBuilder'
	action = 'index'
	attributes = Neos.Fusion:RawArray
	attributes.role = 'form'
	elements = ${q(node).children('elements').get(0)}

	responseElements = Neos.Neos:ContentCollection {
		nodePath = 'responseElements'
	}

	attributes.timehash = ${CRON.TimeHash.getTimeHash()}
	attributes.hasHoneypotField = ${q(node).property('hasHoneypotField')}
	tsPackageKey = 'CRON.FormBuilder'

	hasUploadElement = ${q(node).find('[instanceof CRON.FormBuilder:FileUpload]').count() > 0}
}

elements = Neos.Neos:ContentCollection {
	nodePath = 'elements'
}
